/-
Copyright (c) 2024-2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Katerina Hristova, FrantiÅ¡ek SilvÃ¡Å¡i, Julian Sutherland, Ilia Vlasov
-/

import ArkLib.Data.Polynomial.Bivariate
import ArkLib.Data.Polynomial.Prelims
import Mathlib.Algebra.Field.IsField
import Mathlib.Algebra.Group.Irreducible.Lemmas
import Mathlib.Algebra.GroupWithZero.Associated
import Mathlib.Algebra.Polynomial.Basic
import Mathlib.Algebra.Polynomial.Bivariate
import Mathlib.Algebra.Polynomial.Eval.Defs
import Mathlib.Algebra.Polynomial.FieldDivision
import Mathlib.FieldTheory.RatFunc.Defs
import Mathlib.RingTheory.Ideal.Quotient.Defs
import Mathlib.RingTheory.Ideal.Span
import Mathlib.RingTheory.Polynomial.Content
import Mathlib.RingTheory.Polynomial.GaussLemma
import Mathlib.RingTheory.PowerSeries.Basic
import Mathlib.RingTheory.PowerSeries.Substitution


/-!
  # Definitions and Theorems about Function Fields and Rings of Regular Functions

  We define the notions of Appendix A of [BCIKS20].

  ## References

  * [Ben-Sasson, E., Carmon, D., Ishai, Y., Kopparty, S., and Saraf, S.,
      *Proximity Gaps for Reed-Solomon Codes*][BCIKS20]

  ## Main Definitions

-/

open Polynomial
open Polynomial.Bivariate
open ToRatFunc
open Ideal

namespace BCIKS20AppendixA

section

variable {F : Type} [Field F]

/-- Construction of the monisized polynomial `H_tilde` in Appendix A.1 of [BCIKS20].
Note: Here `H âˆˆ F[X][Y]` translates to `H âˆˆ F[Z][Y]` in [BCIKS20] and H_tilde in
`Polynomial (RatFunc F)` translates to `H_tilde âˆˆ F(Z)[T]` in [BCIKS20]. -/
noncomputable def H_tilde (H : F[X][Y]) : Polynomial (RatFunc F) :=
  let háµ¢ (i : â„•) := H.coeff i
  let d := H.natDegree
  let W := (RingHom.comp Polynomial.C univPolyHom) (háµ¢ d)
  let S : Polynomial (RatFunc F) := Polynomial.X / W
  let H' := Polynomial.evalâ‚‚ (RingHom.comp Polynomial.C univPolyHom) S H
  W ^ (d - 1) * H'

theorem irreducible_comp_C_mul_X_iff {K : Type} [Field K] (a : K) (ha : a â‰  0) (p : K[X]) :
    Irreducible (p.comp (Polynomial.C a * Polynomial.X)) â†” Irreducible p := by
  classical
  let f : K[X] â†’+* K[X] := Polynomial.compRingHom (Polynomial.C a * Polynomial.X)
  let g : K[X] â†’+* K[X] := Polynomial.compRingHom (Polynomial.C aâ»Â¹ * Polynomial.X)
  have hCa : (Polynomial.C aâ»Â¹ * Polynomial.C a : K[X]) = 1 := by
    simpa [Polynomial.C_mul] using (congrArg Polynomial.C (inv_mul_cancelâ‚€ ha))
  have hCb : (Polynomial.C a * Polynomial.C aâ»Â¹ : K[X]) = 1 := by
    simpa [Polynomial.C_mul] using (congrArg Polynomial.C (mul_inv_cancelâ‚€ ha))
  have hlinâ‚ : (Polynomial.C aâ»Â¹ * (Polynomial.C a * Polynomial.X) : K[X]) = Polynomial.X := by
    grind only
  have hlinâ‚‚ : (Polynomial.C a * (Polynomial.C aâ»Â¹ * Polynomial.X) : K[X]) = Polynomial.X := by
    grind only
  have hcompâ‚ :
      ((Polynomial.C aâ»Â¹ * Polynomial.X).comp (Polynomial.C a * Polynomial.X) : K[X]) =
        Polynomial.X := by simp_all only [ne_eq, mul_comp, C_comp, X_comp]
  have hcompâ‚‚ :
      ((Polynomial.C a * Polynomial.X).comp (Polynomial.C aâ»Â¹ * Polynomial.X) : K[X]) =
        Polynomial.X := by simp_all only [ne_eq, mul_comp, C_comp, X_comp]
  have hf : f.comp g = RingHom.id K[X] := by
    refine RingHom.ext ?_
    intro q
    simp [f, g, Polynomial.comp_assoc, hcompâ‚]
  have hg : g.comp f = RingHom.id K[X] := by
    refine RingHom.ext ?_
    intro q
    simp [f, g, Polynomial.comp_assoc, hcompâ‚‚]
  let e : K[X] â‰ƒ+* K[X] := RingEquiv.ofRingHom f g hf hg
  simpa [e, f, Polynomial.coe_compRingHom_apply] using
    (MulEquiv.irreducible_iff (f := (e : K[X] â‰ƒ* K[X])) (x := p))

theorem irreducible_map_univPolyHom_of_irreducible {F : Type} [Field F]
    {H : Polynomial (Polynomial F)} (hdeg : H.natDegree â‰  0) :
    Irreducible H â†’ Irreducible (H.map (ToRatFunc.univPolyHom (F := F))) := by
  intro hH
  classical
  have hprim : H.IsPrimitive := by exact Irreducible.isPrimitive hH hdeg
  have hmap : Irreducible (H.map (algebraMap (Polynomial F) (RatFunc F))) := by
    exact (IsPrimitive.irreducible_iff_irreducible_map_fraction_map hprim).mp hH
  exact hmap

theorem irreducibleHTildeOfIrreducible {F : Type} [Field F] {H : Polynomial (Polynomial F)}
    (hdeg : H.natDegree â‰  0) :
    (Irreducible H â†’ Irreducible (H_tilde H)) := by
  intro hH
  classical
  -- set up the constants appearing in `H_tilde`
  let d : â„• := H.natDegree
  let lc : Polynomial F := H.coeff d
  let a : RatFunc F := ToRatFunc.univPolyHom (F := F) lc
  let W : Polynomial (RatFunc F) := Polynomial.C a

  -- `lc` is nonzero (it is the leading coefficient)
  have hH0 : H â‰  0 := by exact Ne.symm (ne_of_apply_ne natDegree fun a â†¦ hdeg (id (Eq.symm a)))
  have hlc0 : lc â‰  0 := by
    simp_all only [ne_eq, coeff_natDegree, leadingCoeff_eq_zero, not_false_eq_true, lc, d]

  -- hence its image in `RatFunc F` is nonzero
  have ha0 : a â‰  0 := by
    have hinj : Function.Injective (ToRatFunc.univPolyHom (F := F)) := by
      simpa [ToRatFunc.univPolyHom] using (RatFunc.algebraMap_injective (K := F))
    intro ha
    apply hlc0
    apply hinj
    have hmap0 : ToRatFunc.univPolyHom (F := F) lc = 0 := by exact ha
    calc
      ToRatFunc.univPolyHom (F := F) lc = 0 := by exact ha
      _ = ToRatFunc.univPolyHom (F := F) 0 := by simp

  -- irreducibility over `RatFunc F`
  have hHmap : Irreducible (H.map (ToRatFunc.univPolyHom (F := F))) := by
    exact irreducible_map_univPolyHom_of_irreducible hdeg hH

  -- linear change of variables: irreducible `p` implies irreducible `p.comp (C aâ»Â¹ * X)`
  have ha0' : (aâ»Â¹ : RatFunc F) â‰  0 := by exact inv_ne_zero ha0
  have hcomp :
      Irreducible
        ((H.map (ToRatFunc.univPolyHom (F := F))).comp (Polynomial.C (aâ»Â¹) * Polynomial.X)) := by
        exact (irreducible_comp_C_mul_X_iff aâ»Â¹ ha0' (Polynomial.map univPolyHom H)).mpr hHmap

  -- compute `X / W = C aâ»Â¹ * X`
  have hS : (Polynomial.X / W) = Polynomial.C (aâ»Â¹) * Polynomial.X := by
    calc
      Polynomial.X / W = Polynomial.X / Polynomial.C a := by rfl
      _ = Polynomial.X * Polynomial.C (aâ»Â¹) := by exact div_C
        -- simpa using (Polynomial.div_C (p := (Polynomial.X : Polynomial (RatFunc F))) (a := a))
      _ = Polynomial.C (aâ»Â¹) * Polynomial.X := by exact X_mul_C aâ»Â¹

  -- rewrite the evaluation polynomial `H'` as a composition
  have hEval :
      Polynomial.evalâ‚‚
          (RingHom.comp Polynomial.C (ToRatFunc.univPolyHom (F := F))) (Polynomial.X / W) H =
        (H.map (ToRatFunc.univPolyHom (F := F))).comp (Polynomial.X / W) := by
    simpa [Polynomial.comp] using
      (Polynomial.evalâ‚‚_map (p := H) (f := ToRatFunc.univPolyHom (F := F))
            (g := (Polynomial.C : RatFunc F â†’+* Polynomial (RatFunc F)))
            (x := (Polynomial.X / W))).symm

  -- hence the `evalâ‚‚`-polynomial appearing in `H_tilde` is irreducible
  have hH' :
      Irreducible
        (Polynomial.evalâ‚‚ (RingHom.comp Polynomial.C (ToRatFunc.univPolyHom (F := F)))
          (Polynomial.X / W) H) := by grind only

  -- the prefactor `W^(d-1)` is a unit
  have hunitW : IsUnit (W ^ (d - 1)) := by
    have haUnit : IsUnit a := by exact Ne.isUnit ha0
    have hWUnit : IsUnit W := by exact isUnit_C.mpr haUnit
    exact (hWUnit.pow (d - 1))

  rcases hunitW with âŸ¨u, huâŸ©
  have hu' : (u : Polynomial (RatFunc F)) = W ^ (d - 1) := by exact hu

  -- unfold `H_tilde` and finish using `irreducible_units_mul`
  -- (multiplying by a unit does not affect irreducibility)
  -- First, rewrite `H_tilde` into a product with left factor `W^(d-1)`.
  have htilde_unfold :
      H_tilde H =
        (W ^ (d - 1)) *
          (Polynomial.evalâ‚‚ (RingHom.comp Polynomial.C (ToRatFunc.univPolyHom (F := F)))
            (Polynomial.X / W) H) := by rfl

  -- now apply the unit-multiplication lemma
  have hirr_prod :
      Irreducible
        ((W ^ (d - 1)) *
          (Polynomial.evalâ‚‚ (RingHom.comp Polynomial.C (ToRatFunc.univPolyHom (F := F)))
            (Polynomial.X / W) H)) := by
    -- rewrite the left factor as `â†‘u`
    simpa [hu'] using
      (irreducible_units_mul (M := Polynomial (RatFunc F)) (u := u)
          (y :=
            Polynomial.evalâ‚‚ (RingHom.comp Polynomial.C (ToRatFunc.univPolyHom (F := F)))
              (Polynomial.X / W) H)).2
        hH'
  exact hirr_prod

/-- The function field `ğ•ƒ ` from Appendix A.1 of [BCIKS20]. -/
abbrev ğ•ƒ (H : F[X][Y]) : Type :=
  (Polynomial (RatFunc F)) â§¸ (Ideal.span {H_tilde H})

/-- The function field `ğ•ƒ ` is indeed a field if and only if the generator of the ideal we quotient
by is an irreducible polynomial. -/
lemma isField_of_irreducible {H : F[X][Y]} (hdeg : H.natDegree â‰  0) :
    Irreducible H â†’ IsField (ğ•ƒ H) := by
  intro h
  unfold ğ•ƒ
  erw
    [
      â†Ideal.Quotient.maximal_ideal_iff_isField_quotient,
      principal_is_maximal_iff_irred
    ]
  exact irreducibleHTildeOfIrreducible hdeg h

/-- The function field `ğ•ƒ` as defined above is a field. -/
noncomputable instance {H : F[X][Y]} [inst : Fact (Irreducible H)]
    [hdeg : Fact (H.natDegree â‰  0)] : Field (ğ•ƒ H) :=
  IsField.toField (isField_of_irreducible (H := H) hdeg.out inst.out)

/-- The monisized polynomial `H_tilde` is in fact an element of `F[X][Y]`. -/
noncomputable def H_tilde' (H : F[X][Y]) : F[X][Y] :=
  let háµ¢ (i : â„•) := H.coeff i
  let d := H.natDegree
  let W := háµ¢ d
  Polynomial.X ^ d +
    âˆ‘ i âˆˆ (List.range d).toFinset,
      Polynomial.X^(d - 1 - i) *
      Polynomial.C (háµ¢ (d - 1 - i) * W ^ i)

lemma H_tilde_equiv_H_tilde' (H : F[X][Y]) : (H_tilde' H).map univPolyHom = H_tilde H := by
  sorry


/-- The ring of regular elements `ğ’ª` from Appendix A.1 of [BCIKS20]. -/
abbrev ğ’ª (H : F[X][Y]) : Type :=
  (Polynomial (Polynomial F)) â§¸ (Ideal.span {H_tilde' H})

/-- The ring of regular elements field `ğ’ª` is a indeed a ring. -/
noncomputable instance {H : F[X][Y]} : Ring (ğ’ª H) :=
  Ideal.Quotient.ring (Ideal.span {H_tilde' H})

theorem bivPolyHom_HTilde'_eq_HTilde (H : F[X][Y]) :
    (ToRatFunc.bivPolyHom (F := F)) (H_tilde' H) = H_tilde H := by
  simpa [ToRatFunc.bivPolyHom, Polynomial.coe_mapRingHom] using
    (H_tilde_equiv_H_tilde' H)

theorem embeddingOfğ’ªIntoğ•ƒ_ideal_le (H : F[X][Y]) :
    Ideal.span ({H_tilde' H} : Set F[X][Y]) â‰¤
      (Ideal.span ({H_tilde H} : Set (Polynomial (RatFunc F)))).comap
        (ToRatFunc.bivPolyHom (F := F)) := by
  classical
  -- Reduce to showing the generator lies in the comap ideal
  rw [Ideal.span_singleton_le_iff_mem]
  -- Unfold membership in a comap ideal and rewrite using the bridging lemma
  simpa [Ideal.mem_comap, bivPolyHom_HTilde'_eq_HTilde H] using
    (Ideal.subset_span (by
      simp : (H_tilde H) âˆˆ ({H_tilde H} : Set (Polynomial (RatFunc F)))))

/-- The ring homomorphism defining the embedding of `ğ’ª` into `ğ•ƒ`. -/
noncomputable def embeddingOfğ’ªIntoğ•ƒ (H : F[X][Y]) :
    ğ’ª H â†’+* ğ•ƒ H := by
  classical
  refine
    Ideal.quotientMap
      (I := Ideal.span ({H_tilde' H} : Set F[X][Y]))
      (Ideal.span ({H_tilde H} : Set (Polynomial (RatFunc F))))
      (ToRatFunc.bivPolyHom (F := F))
      (embeddingOfğ’ªIntoğ•ƒ_ideal_le H)

/-- The set of regular elements inside `ğ•ƒ H`, i.e. the set of elements of `ğ•ƒ H`
that in fact lie in `ğ’ª H`. -/
def regularElms_set (H : F[X][Y]) : Set (ğ•ƒ H) :=
  {a : ğ•ƒ H | âˆƒ b : ğ’ª H, a = embeddingOfğ’ªIntoğ•ƒ _ b}

/-- The regular elements inside `ğ•ƒ H`, i.e. the elements of `ğ•ƒ H` that in fact lie in `ğ’ª H`
as Type. -/
def regularElms (H : F[X][Y]) : Type :=
  {a : ğ•ƒ H // âˆƒ b : ğ’ª H, a = embeddingOfğ’ªIntoğ•ƒ _ b}

/-- Given an element `z âˆˆ F`, `t_z âˆˆ F` is a rational root of a bivariate polynomial if the pair
`(z, t_z)` is a root of the bivariate polynomial.
-/
def rationalRoot (H : F[X][Y]) (z : F) : Type :=
  {t_z : F // evalEval z t_z H = 0}

/-- The rational substitution `Ï€_z` from Appendix A.3 defined on the whole ring of
bivariate polynomials. -/
noncomputable def Ï€_z_lift {H : F[X][Y]} (z : F) (root : rationalRoot (H_tilde' H) z) :
  F[X][Y] â†’+* F := Polynomial.evalEvalRingHom z root.1

/-- `Ï€_z_lift` annihilates `H_tilde'`. -/
theorem pi_z_lift_H_tilde'_eq_zero {H : F[X][Y]} (z : F)
    (root : rationalRoot (H_tilde' H) z) :
    Ï€_z_lift (H := H) z root (H_tilde' H) = 0 := by
  classical
  simpa [Ï€_z_lift] using root.property

/-- The kernel of `Ï€_z_lift` contains the span of `H_tilde'`. -/
theorem pi_z_lift_span_le_ker {H : F[X][Y]} (z : F)
    (root : rationalRoot (H_tilde' H) z) :
    Ideal.span {H_tilde' H} â‰¤ RingHom.ker (Ï€_z_lift (H := H) z root) := by
  classical
  refine
    (Ideal.span_singleton_le_iff_mem (I := RingHom.ker (Ï€_z_lift (H := H) z root))
          (x := H_tilde' H)).2 ?_
  exact (RingHom.mem_ker).2 (pi_z_lift_H_tilde'_eq_zero (H := H) z root)

/-- `Ï€_z_lift` vanishes on the span of `H_tilde'`. -/
theorem pi_z_lift_vanishes_on_span {H : F[X][Y]} (z : F)
    (root : rationalRoot (H_tilde' H) z) :
    âˆ€ a, a âˆˆ Ideal.span {H_tilde' H} â†’ Ï€_z_lift (H := H) z root a = 0 := by
  intro a ha
  have hker : a âˆˆ RingHom.ker (Ï€_z_lift (H := H) z root) :=
    (pi_z_lift_span_le_ker (H := H) z root) ha
  exact (RingHom.mem_ker (f := Ï€_z_lift (H := H) z root)).1 hker

/-- The rational substitution map `ğ’ª H â†’+* F` obtained by descending `Ï€_z_lift`. -/
noncomputable def Ï€_z {H : F[X][Y]} (z : F) (root : rationalRoot (H_tilde' H) z) :
    ğ’ª H â†’+* F := by
  classical
  refine Ideal.Quotient.lift (Ideal.span {H_tilde' H}) (Ï€_z_lift (H := H) z root) ?_
  intro a ha
  exact pi_z_lift_vanishes_on_span (H := H) z root a ha

/-- The canonical representative of an element of `F[X][Y]` inside
the ring of regular elements `ğ’ª`. -/
noncomputable def canonicalRepOfğ’ª {H : F[X][Y]} (Î² : ğ’ª H) : F[X][Y] :=
  Polynomial.modByMonic Î².out (H_tilde' H)

/-- `Î›` is a weight function on the ring of bivariate polynomials `F[X][Y]`. The weight of
a polynomial is the maximal weight of all monomials appearing in it with non-zero coefficients.
The weight of the zero polynomial is `âˆ’âˆ`.
Requires `D â‰¥ Bivariate.totalDegree H` to match definition in [BCIKS20].
-/
def weight_Î› (f H : F[X][Y]) (D : â„•) : WithBot â„• :=
  Finset.sup
    f.support
    (fun deg =>
      WithBot.some <| deg * (D + 1 - Bivariate.natDegreeY H) + (f.coeff deg).natDegree
    )

/-- The weight function `Î›` on the ring of regular elements `ğ’ª` is defined as the weight their
canonical representatives in `F[X][Y]`. -/
noncomputable def weight_Î›_over_ğ’ª {H : F[X][Y]} (f : ğ’ª H) (D : â„•)
: WithBot â„• := weight_Î› (canonicalRepOfğ’ª f) H D

/-- The set `S_Î²` from the statement of Lemma A.1 in Appendix A of [BCIKS20].
Note: Here `F[X][Y]` is `F[Z][T]`. -/
noncomputable def S_Î² {H : F[X][Y]} (Î² : ğ’ª H) : Set F :=
  {z : F | âˆƒ root : rationalRoot (H_tilde' H) z, (Ï€_z z root) Î² = 0}

/-- The statement of Lemma A.1 in Appendix A.3 of [BCIKS20]. -/
lemma Lemma_A_1 {H : F[X][Y]} (Î² : ğ’ª H) (D : â„•) (hD : D â‰¥ Bivariate.totalDegree H)
    (S_Î²_card : Set.ncard (S_Î² Î²) > (weight_Î›_over_ğ’ª Î² D) * H.natDegree) :
  embeddingOfğ’ªIntoğ•ƒ _ Î² = 0 := by
  sorry

/-- The embeddining of the coefficients of a bivarite polynomial into the bivariate polynomial ring
with rational coefficients. -/
noncomputable def coeffAsRatFunc : F[X] â†’+* Polynomial (RatFunc F) :=
  RingHom.comp bivPolyHom Polynomial.C

/-- The embeddining of the coefficients of a bivarite polynomial into the function field `ğ•ƒ`. -/
noncomputable def liftToFunctionField {H : F[X][Y]} : F[X] â†’+* ğ•ƒ H :=
  RingHom.comp (Ideal.Quotient.mk (Ideal.span {H_tilde H})) coeffAsRatFunc

noncomputable def liftBivariate {H : F[X][Y]} : F[X][Y] â†’+* ğ•ƒ H :=
  RingHom.comp (Ideal.Quotient.mk (Ideal.span {H_tilde H})) bivPolyHom

/-- The embeddining of the scalars into the function field `ğ•ƒ`. -/
noncomputable def fieldToğ•ƒ {H : F[X][Y]} : F â†’+* ğ•ƒ H :=
  RingHom.comp liftToFunctionField Polynomial.C

noncomputable def polyToPowerSeriesğ•ƒ (H : F[X][Y])
  (P : F[X][Y])
    : PowerSeries (ğ•ƒ H) :=
  PowerSeries.mk <| fun n =>
    liftToFunctionField (P.coeff n)


end

noncomputable section

namespace ClaimA2

variable {F : Type} [Field F]
         {R : F[X][X][Y]}
         {H : F[X][Y]} [H_irreducible : Fact (Irreducible H)]
         [H_natDegree_pos : Fact (H.natDegree â‰  0)]

/-- The definition of `Î¶` given in Appendix A.4 of [BCIKS20]. -/
def Î¶ (R : F[X][X][Y]) (xâ‚€ : F) (H : F[X][Y])
    [H_irreducible : Fact (Irreducible H)] [H_natDegree_pos : Fact (H.natDegree â‰  0)] :
    ğ•ƒ H :=
  let W  : ğ•ƒ H := liftToFunctionField (H.leadingCoeff);
  let T : ğ•ƒ H := Ideal.Quotient.mk (Ideal.span {H_tilde H}) Polynomial.X;
    Polynomial.evalâ‚‚ liftToFunctionField (T / W)
      (Bivariate.evalX (Polynomial.C xâ‚€) R.derivative)

/-- There exist regular elements `Î¾ = W(Z)^(d-2) * Î¶` as defined in Claim A.2 of Appendix A.4
of [BCIKS20]. -/
lemma Î¾_regular (xâ‚€ : F) (R : F[X][X][Y]) (H : F[X][Y])
    [H_irreducible : Fact (Irreducible H)] [H_natDegree_pos : Fact (H.natDegree â‰  0)] :
  âˆƒ pre : ğ’ª H,
    let d := R.natDegree
    let W : ğ•ƒ H := liftToFunctionField (H.leadingCoeff);
    embeddingOfğ’ªIntoğ•ƒ _ pre = W ^ (d - 2) * Î¶ R xâ‚€ H := by
  sorry

/-- The elements `Î¾ = W(Z)^(d-2) * Î¶` as defined in Claim A.2 of Appendix A.4 of [BCIKS20]. -/
def Î¾ (xâ‚€ : F) (R : F[X][X][Y]) (H : F[X][Y])
    [Ï† : Fact (Irreducible H)] [H_natDegree_pos : Fact (H.natDegree â‰  0)] : ğ’ª H :=
  (Î¾_regular xâ‚€ R H).choose

/-- The bound of the weight `Î›` of the elements `Î¶` as stated in Claim A.2 of Appendix A.4
of [BCIKS20]. -/
lemma weight_Î¾_bound (xâ‚€ : F) {D : â„•} (hD : D â‰¥ Bivariate.totalDegree H) :
  weight_Î›_over_ğ’ª (Î¾ xâ‚€ R H) D â‰¤
    WithBot.some ((Bivariate.natDegreeY R - 1) * (D - Bivariate.natDegreeY H + 1)) := by
  sorry

/-- There exist regular elements `Î²` with a weight bound as given in Claim A.2
of Appendix A.4 of [BCIKS20]. -/
lemma Î²_regular (R : F[X][X][Y])
                (H : F[X][Y]) [H_irreducible : Fact (Irreducible H)]
                [H_natDegree_pos : Fact (H.natDegree â‰  0)]
                {D : â„•} (hD : D â‰¥ Bivariate.totalDegree H) :
    âˆ€ t : â„•, âˆƒ Î² : ğ’ª H, weight_Î›_over_ğ’ª Î² â‰¤ (2 * t + 1) * Bivariate.natDegreeY R * D := by
  sorry

/-- The definition of the regular elements `Î²` giving the numerators of the Hensel lift coefficients
as defined in Claim A.2 of Appendix A.4 of [BCIKS20]. -/
def Î² (R : F[X][X][Y]) (t : â„•) : ğ’ª H :=
  (Î²_regular R H (Nat.le_refl _) t).choose

/-- The Hensel lift coefficients `Î±` are of the form as given in Claim A.2 of Appendix A.4
of [BCIKS20]. -/
def Î± (xâ‚€ : F) (R : F[X][X][Y]) (H : F[X][Y])
    [Ï† : Fact (Irreducible H)] [H_natDegree_pos : Fact (H.natDegree â‰  0)] (t : â„•) : ğ•ƒ H :=
  let W : ğ•ƒ H := liftToFunctionField (H.leadingCoeff)
  embeddingOfğ’ªIntoğ•ƒ _ (Î² R t) / (W ^ (t + 1) * (embeddingOfğ’ªIntoğ•ƒ _ (Î¾ xâ‚€ R H)) ^ (2*t - 1))

/-- The Hensel lift coefficients `Î±'` with bundled irreducibility witness. -/
def Î±' (xâ‚€ : F) (R : F[X][X][Y]) (H_irreducible : Irreducible H) (t : â„•) : ğ•ƒ H :=
  Î± xâ‚€ R _ (Ï† := âŸ¨H_irreducibleâŸ©) t

/-- The power series `Î³ = âˆ‘ Î±^t (X - xâ‚€)^t âˆˆ ğ•ƒ [[X - xâ‚€]]` as defined in Appendix A.4
of [BCIKS20]. -/
def Î³ (xâ‚€ : F) (R : F[X][X][Y]) (H : F[X][Y])
    [Ï† : Fact (Irreducible H)] [H_natDegree_pos : Fact (H.natDegree â‰  0)] :
    PowerSeries (ğ•ƒ H) :=
  let subst (t : â„•) : ğ•ƒ H :=
    match t with
    | 0 => fieldToğ•ƒ (-xâ‚€)
    | 1 => 1
    | _ => 0
  PowerSeries.subst (PowerSeries.mk subst) (PowerSeries.mk (Î± xâ‚€ R H))

/-- The power series `Î³'` with bundled irreducibility witness. -/
def Î³' (xâ‚€ : F) (R : F[X][X][Y]) (H_irreducible : Irreducible H) : PowerSeries (ğ•ƒ H) :=
  Î³ xâ‚€ R H (Ï† := âŸ¨H_irreducibleâŸ©)

end ClaimA2
end
end BCIKS20AppendixA
